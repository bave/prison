subsystem DSlibinfoMIG 50000;

userprefix libinfoDSmig_;
serverprefix libinfoDSmig_do_;

import <sys/types.h>;

#include <mach/std_types.defs>
#include <mach/mach_types.defs>

import "DSlibinfoMIG_types.h";

type proc_name_t = c_string [ * : 256 ];
type inline_data_t = array [ * : 16384 ] of char;

/*
 * This is the actual API call
 */
 
routine GetProcedureNumber
(
        server                      : mach_port_t;
        name                        : proc_name_t;
    out procno                      : int32_t;
        ServerAuditToken bsmtoken   : audit_token_t;
        UserSecToken usertoken      : security_token_t
);

routine Query
(
        server                      : mach_port_t;
        proc                        : int32_t;
        request                     : inline_data_t;
    out reply                       : inline_data_t;
    out ooreply                     : pointer_t, Dealloc;
        ServerAuditToken bsmtoken   : audit_token_t;
        UserSecToken usertoken      : security_token_t
);

simpleroutine Query_async
(
        server                      : mach_port_t;
        replyToPort                 : mach_port_make_send_once_t;
        proc                        : int32_t;
        request                     : inline_data_t;
        callbackAddr                : mach_vm_address_t;
        ServerAuditToken bsmtoken   : audit_token_t
);
